@go.Package { name = "github.com/Umaaz/redfish/pkg/config" }
@go.Field {
  structTags {
    ["toml"] = "%{name},omitempty"
    ["json"] = "%{name},omitempty"
    ["yaml"] = "%{name},omitempty"
  }
}
abstract module pickle.config.job

import "package://pkg.pkl-lang.org/pkl-go/pkl.golang@0.5.3#/go.pkl"

class RuleConfig {
  rule: String
  job: JobConfig
}

class JobConfig {
  name: String
  tests: Listing<Test>
}

class Test {
  id: String?
  name: String?
  url: String | DataSource
  method: "get" | "post" | "put" | "delete" | String = "get"
  body: RequestBody?

  headers: Mapping<String, String>?
  expected: Expectation = new {}
}

class Expectation {
  status: Int(isBetween(100, 600)) = 200
  headers: Mapping<String, Matcher>?
  body: Matcher?
}

abstract class Matcher {
  name: String
}

class JsonMatcher extends Matcher {
  name = "json"
  expected: Mapping<String, Any>
}

class StringMatcher extends Matcher {
  name = "string"
  expected: String
}

typealias Source = String | DataSource

class DataSource {
  sourceId: String
  source: "response"
  extractor: Extractor
}

abstract class RequestBody {
  type: String
}

class FormBody extends RequestBody {
  type = "form"
  params: Mapping<Source, Source>
}

class JsonBody extends RequestBody {
  type = "json"
  params: Mapping<Source, Source>
}

abstract class Extractor {
  default: String = ""
}

class JsonExtractor extends Extractor {
  path: String
}